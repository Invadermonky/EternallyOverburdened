apply from: 'gradle/scripts/helpers.gradle'

repositories {
    // Other repositories described by default:
    // CleanroomMC: https://maven.cleanroommc.com
    exclusiveContent {
        forRepository {
            maven {
                name 'CurseMaven'
                url 'https://cursemaven.com'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'Modrinth'
                url 'https://api.modrinth.com/maven'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url 'https://maven.blamejared.com/'
            }
        }
        filter {
            includeGroup 'CraftTweaker2'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://maven.tterrag.com"
            }
        }
        filter {
            includeGroup 'mcjty.theoneprobe'
        }
    }
    mavenLocal() // Must be last for caching to work
}

dependencies {
    // Include StripLatestForgeRequirements by default for the dev env, saves everyone a hassle
    runtimeOnly 'com.cleanroommc:strip-latest-forge-requirements:1.0'
    // Include OSXNarratorBlocker by default for the dev env, for M1+ Macs
    runtimeOnly 'com.cleanroommc:osxnarratorblocker:1.0'
    compileOnly 'org.jetbrains:annotations:24.1.0'

    runtimeOnly rfg.deobf("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17")

    implementation 'mezz:jei:4.27.3'
    implementation rfg.deobf('curse.maven:baubles-227083:2518667')

    implementation "CraftTweaker2:CraftTweaker2-API:4.1.20.695"
    implementation "CraftTweaker2:ZenScript:4.1.20.695"
    implementation "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.695"
    implementation("com.cleanroommc:groovyscript:1.2.2") { transitive = false }

    // Mod Integrations
    def modMap = [:]
    def loadMap = [:]

    modMap.put(["curse.maven:actually-additions-228404:3117927"], project.use_actuallyadditions)
    modMap.put(["curse.maven:hbm-nuclear-tech-mod-community-edition-1312314:7415696"], project.use_hbm)
    modMap.put(["curse.maven:immersive-engineering-231951:2974106"], project.use_immersiveengineering)
    modMap.put(["curse.maven:quark-rotn-edition-417392:5594720", "curse.maven:autoreglib-250363:2746011"], project.use_quark)
    modMap.put(["mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17"], project.use_theoneprobe)
    modMap.put(["curse.maven:travelers-backpack-321117:3150850"], project.use_travelersbackpack)

    modMap.each{mods, load -> {
        boolean loadFlag = load.toBoolean()
        mods.each{mod ->
            if(!loadMap.containsKey(mod)) {
                loadMap.put(mod,loadFlag)
            } else {
                if(loadFlag && !loadMap[mod]) {
                    loadMap[mod] = loadFlag
                }
            }
        }
    }}

    loadMap.each{maven,load -> {
        if(load) {
            implementation(rfg.deobf(maven))
        } else {
            compileOnly(rfg.deobf(maven))
        }
    }}
}